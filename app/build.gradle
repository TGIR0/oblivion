plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = 'org.bepass.oblivion'
    compileSdk = 35
    ndkVersion = "29.0.14206865"
    defaultConfig {
        applicationId = "org.bepass.oblivion"
        minSdk = 24
        targetSdk = 35
        versionCode = 18
        versionName = "8"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }
    androidResources {
        generateLocaleConfig = true
    }
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // signingConfig signingConfigs.debug // SECURITY: Do not use debug keys for release. Configure a proper release keystore.
        }
    }
    compileOptions {
        // Bytecode level for Java
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        // Bytecode level for Kotlin (must match Java)
        jvmTarget = '17'
    }
    java {
        toolchain {
            // JDK used to run the compiler (you can point this to JDK 25 on your system)
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

// Suppress noisy Java deprecation warnings in compiler output while keeping behavior unchanged
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:-deprecation"
}

configurations {
    configureEach {
        exclude group: 'org.json', module: 'json'
        resolutionStrategy {
            force 'androidx.core:core:1.15.0'
            force 'androidx.core:core-ktx:1.15.0'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.github.erfansn:locale-config-x:1.1.0'
    implementation 'com.tencent:mmkv:2.0.2'
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
}

static def findGoExecutable() {
    def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
    return isWindows ? "go.exe" : "go"
}

def goExec = findGoExecutable()
def libsDir = file("libs")
def tun2socksAar = file("libs/tun2socks.aar")

tasks.register("preBuildTun2SocksAar", Exec) {
    workingDir "../tun2socks"
    environment "CGO_ENABLED", "1"
    commandLine goExec, "run", "golang.org/x/mobile/cmd/gomobile", "init"
}

tasks.register("buildTun2SocksAar", Exec) {
    doFirst {
        if (!libsDir.exists()) {
            libsDir.mkdirs()
        }
    }

    workingDir "../tun2socks"
    environment "CGO_ENABLED", "1"
    commandLine goExec, "run", "golang.org/x/mobile/cmd/gomobile", "bind",
            "-ldflags=-w -s",
            "-target=android",
            "-androidapi=$android.defaultConfig.minSdk",  // Updated to minSdk 24
            "-o=${tun2socksAar.absolutePath}", "."

    dependsOn(preBuildTun2SocksAar)
}

if (!tun2socksAar.exists() || (project.hasProperty("forceTun2socksRebuild") && project.property("forceTun2socksRebuild").toString().toBoolean())) {
    preBuild.dependsOn(buildTun2SocksAar)
}