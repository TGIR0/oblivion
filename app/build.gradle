plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = 'org.bepass.oblivion'
    compileSdk = 36
    ndkVersion = "29.0.14206865"
    defaultConfig {
        applicationId = "org.bepass.oblivion"
        minSdk = 24
        targetSdk = 36
        versionCode = 18
        versionName = "8"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword "password"
            keyAlias "key0"
            keyPassword "password"
        }
    }

    androidResources {
        generateLocaleConfig = true
    }
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding = true
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
    }


    compileOptions {
        // Bytecode level for Java
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }
    java {
        toolchain {
            // JDK used to run the compiler (you can point this to JDK 25 on your system)
            languageVersion.set(JavaLanguageVersion.of(25))
        }
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_25
    }
}

androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.each { output ->
            output.outputFileName.set("Oblivion.apk")
        }
    }
}

// Suppress noisy Java deprecation warnings in compiler output while keeping behavior unchanged
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:-deprecation"
}

configurations {
    configureEach {
        exclude group: 'org.json', module: 'json'
        resolutionStrategy {
            force 'androidx.core:core:1.17.0'
            force 'androidx.core:core-ktx:1.17.0'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.14.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.github.erfansn:locale-config-x:1.1.0'
    implementation 'com.tencent:mmkv:2.2.4'
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
}

static def findGoExecutable() {
    def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
    return isWindows ? "go.exe" : "go"
}

def goExec = findGoExecutable()
def libsDir = file("libs")
def tun2socksAar = file("libs/tun2socks.aar")

tasks.register("preBuildTun2SocksAar", Exec) {
    workingDir "../tun2socks"
    environment "CGO_ENABLED", "1"
    commandLine goExec, "run", "golang.org/x/mobile/cmd/gomobile", "init"
}

tasks.register("buildTun2SocksAar", Exec) {
    doFirst {
        if (!libsDir.exists()) {
            libsDir.mkdirs()
        }
    }

    workingDir "../tun2socks"
    environment "CGO_ENABLED", "1"
    commandLine goExec, "run", "golang.org/x/mobile/cmd/gomobile", "bind",
            "-ldflags=-w -s",
            "-target=android",
            "-androidapi=$android.defaultConfig.minSdk",
            "-o=${tun2socksAar.absolutePath}", "."

    dependsOn(preBuildTun2SocksAar)
}

if (!tun2socksAar.exists() || (project.hasProperty("forceTun2socksRebuild") && project.property("forceTun2socksRebuild").toString().toBoolean())) {
    preBuild.dependsOn(buildTun2SocksAar)
}